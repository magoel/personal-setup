#!/bin/csh -b
#
# .cshrc:  Commands executed for each new csh
#
set osname = `uname -s`
set version = `uname -r`
set autologout=0


alias addPathEnd 'if (-d \!:1) setenv PATH $PATH\:\!:1'
alias addPathStart 'if (-d \!:1) setenv PATH \!:1\:$PATH'
alias addLibEnd 'if (-d \!:1) setenv LD_LIBRARY_PATH $LD_LIBRARY_PATH\:\!:1'
alias addLibStart 'if (-d \!:1) setenv LD_LIBRARY_PATH  \!:1\:$LD_LIBRARY_PATH'

switch ($osname)
		case HP-UX:
			switch ($version)
				case B.*:
					if ( -e $HOME/.cshrc.hp ) then
						source $HOME/.cshrc.hp
					endif
				breaksw
			endsw	
		breaksw
		case SunOS:
			switch ($version)
				case 5.*:
					if ( -e $HOME/.cshrc.solaris ) then
						source ~$USER/.cshrc.solaris
					endif
				breaksw
			endsw
		breaksw
		case Linux:
			switch ($version)
				case 2.*:
					if ( -e $HOME/.cshrc.linux ) then
						source ~$USER/.cshrc.linux
					endif
				breaksw
			endsw
		breaksw
		default:
			echo "Unable to Source Environment"
			exit 1
endsw
